
%macro BRR_REG(INFILE=,
			   REPLI_ROOT=,
			   VARDEP=,
			   EXPLICA=,
			   BYVAR=,
			   LIMIT=,
			   LIMIT_CRITERIA=,
			   ID_SCHOOL=,
			   OUTFILE=);

/*

MEANING OF THE MACRO ARGUMENTS

INFILE			= 	INPUT DATA FILE.
REPLI_ROOT		= 	ROOT OF THE FINAL WEIGHT AND 80 REPLICATES VARIABLE NAMES. 
					FINAL WEIGHT VARIABLE NAME MUST BE THE REPLICATION ROOT FOLLOWED BY 0.
VARDEP			= 	DEPENDENT VARIABLE 
EXPLICA			=	LIST OF INDEPENDENT VARIABLES
BYVAR			= 	BREAKDOWN VARIABLES
LIMIT          	=   FLAGGING  YES OR NO
LIMIT_CRITERIA 	=   1) NUMBER OF STUDENTS 2) NUMBER OF SCHOOLS 3) PERCENTAGE OF STUDENTS AND 4) NUMBER OF VARIABLES FROM 
					THE BYVAR ARGUMENT FOR DEFINING THE POPULATION OF REFERENCE 
ID_SCHOOL	   	=	VARIABLE NAME FOR THE SCHOOL IDENTIFICATION
OUTFILE			=	FILE WITH THE STATISTIC ESTIMATES AND THEIR STANDARD ERROR ESTIMATE.

*/

OPTIONS NONOTES;

PROC DATASETS LIBRARY=WORK NOLIST;
	DELETE BRR_TEMP1;
RUN;

PROC SORT DATA=&INFILE OUT=BRRDATA (KEEP=&REPLI_ROOT.0-&REPLI_ROOT.80 &BYVAR &VARDEP &EXPLICA &ID_SCHOOL);
	BY &BYVAR;
RUN;

%DO I = 0 %TO 80;

	PROC REG DATA=BRRDATA OUTEST=COEF_TEMP NOPRINT EDF;
		MODEL &VARDEP=&EXPLICA;
		WEIGHT &REPLI_ROOT&I;
		BY &BYVAR;
	RUN;

	DATA COEF_TEMP;
		SET COEF_TEMP;
		L=&I;
	RUN;

	PROC APPEND BASE = BRR_TEMP1 DATA=COEF_TEMP;
 	RUN;
%END;

PROC SORT DATA=BRR_TEMP1;
	BY &BYVAR L;
RUN;

PROC TRANSPOSE DATA=BRR_TEMP1 OUT=BRR_TEMP;
	BY &BYVAR L;
	VAR INTERCEPT &EXPLICA _RSQ_;
RUN; 

DATA BRR_TEMP1 ;
	SET BRR_TEMP (RENAME=(_NAME_=CLASS COL1=COEF));
RUN;

%LET I=1;
%DO %WHILE(%LENGTH(%SCAN(&EXPLICA,&I)));
	%LET I=%EVAL(&I+1);
%END;
%LET NB=%EVAL(&I-1);

%DO J=0 %to &NB;
	%IF &J=0 %THEN %DO;
		%LET INDEP=Intercept;
	%END;
	%IF &J>0 %THEN %DO;
		%LET INDEP=%SCAN(&EXPLICA,&J);
	%END;

	DATA BRR_TEMP1;
		SET BRR_TEMP1;
		IF (UPCASE(CLASS)=UPCASE("&INDEP")) THEN ORDRE=&J;
		IF (SUBSTR(CLASS,1,5)="_RSQ_") THEN ORDRE=&I;
	RUN;
%END;

DATA BRR_TEMP2(KEEP=&BYVAR CLASS COEF) BRR_TEMP3(KEEP=&BYVAR CLASS ORDRE STAT);
	SET BRR_TEMP1;
	IF L > 0 THEN OUTPUT BRR_TEMP2;     
	ELSE DO;
		STAT =COEF;
		OUTPUT BRR_TEMP3;
	END;
RUN;

PROC SORT DATA=BRR_TEMP2;
	BY &BYVAR CLASS;
RUN;

PROC SORT DATA=BRR_TEMP3;
	BY &BYVAR CLASS;
RUN;

DATA BRR_TEMP4;
	MERGE BRR_TEMP2 BRR_TEMP3;
	BY &BYVAR CLASS;
	VARI=((COEF-STAT)**2)*(1/20);
RUN;

PROC UNIVARIATE DATA=BRR_TEMP4 NOPRINT;
	VAR VARI;
	BY &BYVAR CLASS;
	OUTPUT OUT=BRR_TEMP5 SUM=SS;
RUN;

DATA BRR_TEMP6;
	MERGE BRR_TEMP3 BRR_TEMP5;
	BY &BYVAR CLASS;
	SESTAT=(SS)**0.5;
	FORMAT STAT F10.2;
	FORMAT SESTAT F10.2;
	KEEP &BYVAR CLASS ORDRE STAT SESTAT;
RUN;

PROC SORT DATA=BRR_TEMP6 OUT=&OUTFILE (DROP=ORDRE);
	BY &BYVAR ORDRE;
RUN;

%IF (%UPCASE(&LIMIT)=YES) %THEN %DO;

	DATA BRR_TEMP7;
		SET BRRDATA;
		NB_MISS=0;
		ARRAY LIST_VAR (&I) &EXPLICA &VARDEP;
		DO K=1 TO &I;
			IF (LIST_VAR(K) IN (.,.I,.M,.N)) THEN NB_MISS=NB_MISS+1;
		END;
		IF (NB_MISS>1) THEN NB_MISS=1;
	RUN;
	PROC FREQ DATA=BRR_TEMP7 NOPRINT;
		TABLE NB_MISS /OUT=BRR_TEMP8;
		BY &BYVAR;
		WHERE (NB_MISS=0);
	RUN;
	%LET FLAG_STUD=%SCAN(&LIMIT_CRITERIA,1);
	DATA BRR_TEMP8;
		SET BRR_TEMP8;
		IF (COUNT < &FLAG_STUD) THEN FLAG_STUD=1;
		ELSE FLAG_STUD=0;
		KEEP &BYVAR FLAG_STUD;
	RUN;
	PROC SORT DATA=BRR_TEMP7;
		BY &BYVAR &ID_SCHOOL;
	RUN;
	PROC FREQ DATA=BRR_TEMP7 NOPRINT;
		TABLE NB_MISS /OUT=BRR_TEMP9;
		BY &BYVAR &ID_SCHOOL;
		WHERE (NB_MISS=0);
	RUN;		
	PROC FREQ DATA=BRR_TEMP9 NOPRINT;
		TABLE NB_MISS /OUT=BRR_TEMP10;
		BY &BYVAR;
	RUN;
	%LET FLAG_SCH=%SCAN(&LIMIT_CRITERIA,2);
	DATA BRR_TEMP10;
		SET BRR_TEMP10;
		IF (COUNT < &FLAG_SCH) THEN FLAG_SCH=1;
		ELSE FLAG_SCH=0;
		KEEP &BYVAR FLAG_SCH;
	RUN;
	PROC SORT DATA=BRR_TEMP7;
		BY &BYVAR NB_MISS;
	RUN;
	PROC FREQ DATA=BRR_TEMP7 NOPRINT;
		TABLE NB_MISS/OUT=BRR_TEMP11;
		BY &BYVAR;
		WEIGHT &REPLI_ROOT.0;
	RUN;
	%LET K=1;
	%LET POPREF=;
	%LET NBVAR=%SCAN(&LIMIT_CRITERIA,4);
	%DO %WHILE(&K <= &NBVAR);
		%LET POPREF_ADD=%SCAN(&BYVAR,&K);
		%LET POPREF=&POPREF &POPREF_ADD;
		%LET K=%EVAL(&K+1);
	%END;
	
	PROC MEANS DATA=BRR_TEMP11 NOPRINT;
		VAR COUNT;
		BY &POPREF;
		OUTPUT OUT=BRR_TEMP12 SUM=SOMWGT;
	RUN;
	%LET FLAG_PCT=%SCAN(&LIMIT_CRITERIA,3);
	DATA BRR_TEMP13;
		MERGE BRR_TEMP11 BRR_TEMP12;
		BY &POPREF;
		PCT=(COUNT/SOMWGT)*100;
		IF (PCT < &FLAG_PCT) THEN FLAG_PCT=1;
		ELSE FLAG_PCT=0;
		IF (NB_MISS=0);
		KEEP &BYVAR FLAG_PCT;
	RUN;
	DATA &OUTFILE._CRITERIA;
		MERGE BRR_TEMP8 BRR_TEMP10 BRR_TEMP13;
		BY &BYVAR;
	RUN;
	PROC DATASETS LIBRARY=WORK NOLIST;
		DELETE BRR_TEMP7 BRR_TEMP8 BRR_TEMP9 BRR_TEMP10 BRR_TEMP11 BRR_TEMP12 BRR_TEMP13;
	RUN;
%END;

PROC DATASETS LIBRARY=WORK NOLIST;
	DELETE BRR_TEMP BRR_TEMP1 BRR_TEMP2 BRR_TEMP3 BRR_TEMP4 BRR_TEMP5 BRR_TEMP6 COEF_TEMP BRRDATA;
RUN;

OPTIONS NOTES;

%MEND BRR_REG;
 
