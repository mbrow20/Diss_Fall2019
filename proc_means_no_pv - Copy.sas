%MACRO BRR_PROCMEAN(INFILE =,
                    REPLI_ROOT =,
                    BYVAR = ,
                    VAR =,
                    STAT =,
			   		LIMIT=,
			   		LIMIT_CRITERIA=,
			   		ID_SCHOOL=,
                    OUTFILE =);

/* 

MEANING OF THE MACRO ARGUMENTS

INFILE			= 	INPUT DATA FILE.
REPLI_ROOT		= 	ROOT OF THE FINAL WEIGHT AND 80 REPLICATES VARIABLE NAMES. 
					FINAL WEIGHT VARIABLE NAME MUST BE THE REPLICATION ROOT FOLLOWED BY 0.
BYVAR			= 	BREAKDOWN VARIABLES
VAR				= 	VARIABLE ON WHICH THE REQUESTED STATISTIC WILL BE COMPUTED
STAT			= 	REQUESTED STATISTIQUE
						SUMWGT		=	SUM OF THE WEIGHT
						MEAN		= 	MEAN
						VAR			=	VARIANCE
						STD			=	STANDARD DEVIATION
						CV			=	COEFFICIENT OF VARIATION
						MEDIAN		=	MEDIAN
						Q1			=	FIRST QUARTILE
						Q3			=	THIRD QUARTILE
						QRANGE		=	RANGE BETWEEN Q1 AND Q3
						PX			=	PERCENTILE, WITH X BETWEEN 1 AND 99
LIMIT          	=   FLAGGING  YES OR NO
LIMIT_CRITERIA 	=   1) NUMBER OF STUDENTS 2) NUMBER OF SCHOOLS 3) PERCENTAGE OF STUDENTS AND 4) NUMBER OF VARIABLES FROM 
					THE BYVAR ARGUMENT FOR DEFINING THE POPULATION OF REFERENCE 
ID_SCHOOL	   	=	VARIABLE NAME FOR THE SCHOOL IDENTIFICATION
OUTFILE			= 	FILE WITH THE STATISTIC ESTIMATES AND THEIR STANDARD ERROR ESTIMATE

*/ 

OPTIONS NONOTES;

PROC DATASETS LIBRARY=WORK NOLIST;
	DELETE BRR_TEMP1;
RUN;

PROC SORT DATA=&INFILE OUT=BRRDATA(KEEP=&REPLI_ROOT.0-&REPLI_ROOT.80 &BYVAR &VAR &ID_SCHOOL);
  BY &BYVAR;
RUN;

%DO I = 0 %TO 80;
 	PROC MEANS DATA=BRRDATA VARDEF=WGT NOPRINT;
		VAR &VAR ;
		BY &BYVAR;
		WEIGHT &REPLI_ROOT&I;
		OUTPUT OUT=MEAN_TEMP &STAT=PV;
	RUN;

	DATA MEAN_TEMP;
		SET MEAN_TEMP;
		L=&I;
	RUN;

	PROC APPEND BASE = BRR_TEMP1 DATA=MEAN_TEMP;
 	RUN;
%END;

DATA BRR_TEMP2(KEEP=&BYVAR PV)BRR_TEMP3(KEEP=&BYVAR STAT);
	SET BRR_TEMP1;
	IF L > 0 THEN OUTPUT BRR_TEMP2;     
	ELSE DO;
		STAT =PV;
		OUTPUT BRR_TEMP3;
	END;
RUN;

PROC SORT DATA=BRR_TEMP2;
	BY &BYVAR;
RUN;

PROC SORT DATA=BRR_TEMP3;
	BY &BYVAR;
RUN;

DATA BRR_TEMP4;
	MERGE BRR_TEMP2 BRR_TEMP3;
	BY &BYVAR;
	VARI=((PV-STAT)**2)*(1/20);
RUN;

PROC UNIVARIATE DATA=BRR_TEMP4 NOPRINT;
	VAR VARI;
	BY &BYVAR;
	OUTPUT OUT=BRR_TEMP5 SUM=SS;
RUN;


DATA BRR_TEMP6;
	MERGE BRR_TEMP3 BRR_TEMP5;
	BY &BYVAR;
	SESTAT=(SS)**0.5;
	FORMAT STAT F10.2;
	FORMAT SESTAT F17.9;
	KEEP &BYVAR STAT SESTAT;
RUN;

%IF (%UPCASE(&LIMIT)=NO) %THEN %DO;

	DATA &OUTFILE;
		SET BRR_TEMP6;
	RUN;

%END;
%ELSE %DO;
	DATA BRR_TEMP7;
		SET BRRDATA;
		NB_MISS=0;
		ARRAY LIST_VAR (1) &VAR;
		DO K=1 TO 1;
			IF (LIST_VAR(K) IN (.,.I,.M,.N)) THEN NB_MISS=NB_MISS+1;
		END;
		IF (NB_MISS>1) THEN NB_MISS=1;
	RUN;
	PROC FREQ DATA=BRR_TEMP7 NOPRINT;
		TABLE NB_MISS /OUT=BRR_TEMP8;
		BY &BYVAR;
		WHERE (NB_MISS=0);
	RUN;
	%LET FLAG_STUD=%SCAN(&LIMIT_CRITERIA,1);
	DATA BRR_TEMP8;
		SET BRR_TEMP8;
		IF (COUNT < &FLAG_STUD) THEN FLAG_STUD=1;
		ELSE FLAG_STUD=0;
		KEEP &BYVAR FLAG_STUD;
	RUN;
	PROC SORT DATA=BRR_TEMP7;
		BY &BYVAR &ID_SCHOOL;
	RUN;
	PROC FREQ DATA=BRR_TEMP7 NOPRINT;
		TABLE NB_MISS /OUT=BRR_TEMP9;
		BY &BYVAR &ID_SCHOOL;
		WHERE (NB_MISS=0);
	RUN;		
	PROC FREQ DATA=BRR_TEMP9 NOPRINT;
		TABLE NB_MISS /OUT=BRR_TEMP10;
		BY &BYVAR;
	RUN;
	%LET FLAG_SCH=%SCAN(&LIMIT_CRITERIA,2);
	DATA BRR_TEMP10;
		SET BRR_TEMP10;
		IF (COUNT < &FLAG_SCH) THEN FLAG_SCH=1;
		ELSE FLAG_SCH=0;
		KEEP &BYVAR FLAG_SCH;
	RUN;
	PROC SORT DATA=BRR_TEMP7;
		BY &BYVAR NB_MISS;
	RUN;
	PROC FREQ DATA=BRR_TEMP7 NOPRINT;
		TABLE NB_MISS/OUT=BRR_TEMP11;
		BY &BYVAR;
		WEIGHT &REPLI_ROOT.0;
	RUN;
	%LET K=1;
	%LET POPREF=;
	%LET NBVAR=%SCAN(&LIMIT_CRITERIA,4);
	%DO %WHILE(&K <= &NBVAR);
		%LET POPREF_ADD=%SCAN(&BYVAR,&K);
		%LET POPREF=&POPREF &POPREF_ADD;
		%LET K=%EVAL(&K+1);
	%END;
	
	PROC MEANS DATA=BRR_TEMP11 NOPRINT;
		VAR COUNT;
		BY &POPREF;
		OUTPUT OUT=BRR_TEMP12 SUM=SOMWGT;
	RUN;
	%LET FLAG_PCT=%SCAN(&LIMIT_CRITERIA,3);
	DATA BRR_TEMP13;
		MERGE BRR_TEMP11 BRR_TEMP12;
		BY &POPREF;
		PCT=(COUNT/SOMWGT)*100;
		IF (PCT < &FLAG_PCT) THEN FLAG_PCT=1;
		ELSE FLAG_PCT=0;
		IF (NB_MISS=0);
		KEEP &BYVAR FLAG_PCT;
	RUN;
	DATA &OUTFILE;
		MERGE BRR_TEMP6 BRR_TEMP8 BRR_TEMP10 BRR_TEMP13;
		BY &BYVAR;
	RUN;
	PROC DATASETS LIBRARY=WORK NOLIST;
		DELETE BRR_TEMP7 BRR_TEMP8 BRR_TEMP9 BRR_TEMP10 BRR_TEMP11 BRR_TEMP12 BRR_TEMP13;
	RUN;

%END;

PROC DATASETS LIBRARY=WORK NOLIST;
	DELETE BRR_TEMP1 BRR_TEMP2 BRR_TEMP3 BRR_TEMP4 BRR_TEMP5 BRR_TEMP6 MEAN_TEMP BRRDATA;
RUN;

OPTIONS NOTES;

%MEND BRR_PROCMEAN;
