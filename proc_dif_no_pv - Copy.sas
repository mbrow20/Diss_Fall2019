%MACRO BRR_PROCMEAN_DIF(INFILE =,
                        REPLI_ROOT =,
                        BYVAR =,
                        VAR =,
                        COMPARE =,
	                    CATEGORY =,
                        STAT =,
                        OUTFILE =);

/*

MEANING OF THE MACRO ARGUMENTS

INFILE		= 	INPUT DATA FILE.
REPLI_ROOT	= 	ROOT OF THE FINAL WEIGHT AND 80 REPLICATES VARIABLE NAMES. 
				FINAL WEIGHT VARIABLE NAME MUST BE THE REPLICATION ROOT FOLLOWED BY 0.
BYVAR		= 	BREAKDOWN VARIABLES
VAR			= 	VARIABLES FOR WHIXH THE STATISTIC IS REQUESTED
COMPARE		=	BREAKDOWN VARIABLE NAME FOR WICH CATEGORY CONTRASTS ARE REQUESTED
CATEGORY	=	LIST OF THE "COMPARE" VARIABLE CATEGORIES FOR WHICH A CONTRAT IS REQUESTED
STAT		= 	REQUESTED STATISTIQUE
				SUMWGT		=	SUM OF THE WEIGHT
				MEAN		= 	MEAN
				VAR			=	VARIANCE
				STD			=	STANDARD DEVIATION
				CV			=	COEFFICIENT OF VARIATION
				SKEWNESS	=	SKEWNESS
				KURTOSIS	=	KURTOSIS
				MEDIAN		=	MEDIAN
				Q1			=	FIRST QUARTILE
				Q3			=	THIRD QUARTILE
				QRANGE		=	RANGE BETWEEN Q1 AND Q3
				PX			=	PERCENTILE, WITH X BETWEEN 1 AND 99
OUTFILE		= 	FILE WITH THE STATISTIC ESTIMATES AND THEIR STANDARD ERROR ESTIMATE

*/ 
OPTIONS NONOTES;

PROC DATASETS LIBRARY=WORK NOLIST;
	DELETE BRR_TEMP0 BRR_TEMP1 ;
RUN;

PROC SORT DATA=&INFILE OUT=BRRDATA(KEEP=&REPLI_ROOT.0-&REPLI_ROOT.80 &BYVAR &COMPARE &VAR);
	BY &BYVAR &COMPARE;
RUN;

%DO I = 0 %TO 80;

	PROC MEANS DATA=BRRDATA VARDEF=WGT NOPRINT;
		VAR &VAR ;
		BY &BYVAR &COMPARE;
		WEIGHT &REPLI_ROOT&I;
		OUTPUT OUT=MEAN_TEMP &STAT=PV;
	RUN;

	DATA MEAN_TEMP;
		SET MEAN_TEMP;
		L=&I;
	RUN;

	PROC APPEND BASE = BRR_TEMP0 DATA=MEAN_TEMP;
	RUN;
%END;

%LET DEBUT=1;
%LET SUIVANT=2;
%LET COMPTE=1;

%LET I=1;

%DO %WHILE(%LENGTH(%SCAN(&CATEGORY,&I)));
	%LET I=%EVAL(&I+1);
%END;

%LET NBCAT=%EVAL(&I-1);
%LET NBDIF=%EVAL((&NBCAT*(&NBCAT-1))/2);
%LET ORDRE=1;

%DO J=1 %TO &NBDIF;

	%DO K=&DEBUT %TO &NBCAT-1;

		%LET CAT1=%SCAN(&CATEGORY,&DEBUT);
		%LET CAT2=%SCAN(&CATEGORY,&SUIVANT);

		DATA BRR_DIF1;
			 SET BRR_TEMP0 (RENAME=(PV=M1PV));
			 LENGTH CONTRAST $5;
			 CONTRAST="&CAT1.-&CAT2";
			 IF (&COMPARE=&CAT1);
			 KEEP &BYVAR L CONTRAST M1PV &compare;
		RUN;

		DATA BRR_DIF2 ;
		 	 SET BRR_TEMP0 (RENAME=(PV=M2PV));
		     IF (&COMPARE=&CAT2);
			 KEEP &BYVAR L M2PV &compare;
		RUN;

		PROC SORT DATA=BRR_DIF1;
			BY &BYVAR L;
		RUN;

		PROC SORT DATA=BRR_DIF2;
			BY &BYVAR L;
		RUN;

		DATA BRR_TEMP_DIF;
			MERGE BRR_DIF1 BRR_DIF2;
			BY &BYVAR L;
			IF (M1PV EQ . OR M2PV EQ .) THEN DELETE;
			PV=M1PV-M2PV;
			ORDRE=&ORDRE;
			KEEP &BYVAR CONTRAST L PV ORDRE;
		RUN;

 		PROC APPEND BASE = BRR_TEMP1 DATA=BRR_TEMP_DIF;
		RUN;

		%LET SUIVANT=%EVAL(&SUIVANT+1);
		%LET COMPTE=%EVAL(&COMPTE+1);
		%LET ORDRE=%EVAL(&ORDRE+1);

	%END;

	%LET DEBUT=%EVAL(&DEBUT+1);
	%LET SUIVANT=%EVAL(&DEBUT+1);

%END;

DATA BRR_TEMP2(KEEP=&BYVAR CONTRAST PV) BRR_TEMP3(KEEP=&BYVAR CONTRAST ORDRE STAT);
	SET BRR_TEMP1;
	IF L > 0 THEN OUTPUT BRR_TEMP2;     
	ELSE DO;
		STAT =PV;
		OUTPUT BRR_TEMP3;
	END;
RUN;

PROC SORT DATA=BRR_TEMP2;
	BY &BYVAR CONTRAST;
RUN;

PROC SORT DATA=BRR_TEMP3;
	BY &BYVAR CONTRAST;
RUN;

DATA BRR_TEMP4;
	MERGE BRR_TEMP2 BRR_TEMP3;
	BY &BYVAR CONTRAST;
	VARI=((PV-STAT)**2)*(1/20);
RUN;

PROC UNIVARIATE DATA=BRR_TEMP4 NOPRINT;
	VAR VARI;
	BY &BYVAR CONTRAST;
	OUTPUT OUT=BRR_TEMP5 SUM=SS;
RUN;

DATA BRR_TEMP6;
	MERGE BRR_TEMP3 BRR_TEMP5;
	BY &BYVAR CONTRAST;
	SESTAT=(SS)**0.5;
	FORMAT STAT F10.2;
	FORMAT SESTAT F10.2;
	KEEP &BYVAR CONTRAST ORDRE STAT SESTAT;
RUN;

PROC SORT DATA=BRR_TEMP6 OUT=&OUTFILE (DROP=ORDRE);
	BY &BYVAR ORDRE;
RUN;

PROC DATASETS LIBRARY=WORK NOLIST;
	DELETE BRR_TEMP0 BRR_TEMP1 BRR_TEMP2 BRR_TEMP3 BRR_TEMP4 BRR_TEMP5 BRR_TEMP6
	MEAN_TEMP BRRDATA BRR_DIF1 BRR_DIF2 BRR_TEMP_DIF;
RUN;

OPTIONS NOTES;

%MEND BRR_PROCMEAN_DIF;

